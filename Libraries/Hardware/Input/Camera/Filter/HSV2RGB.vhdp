/*
    Name: HSV 2 RGB
    Rev: 1.0
    Creator: Leon Beier
    Date: 25.11.2021
    Copyright (c) 2020 Protop Solutions UG. All right reserved.
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of
    this hdl code and associated documentation files (the "HDL Code"), to deal in the
    HDL Code without restriction, including without limitation the rights to use,
    copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
    HDL Code, and to permit persons to whom the HDL Code is furnished to do so,
    subject to the following conditions:

    The above copyright notice and this permission notice shall be included in all
    copies or substantial portions of the HDL Code.

    THE HDL Code IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
    FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
    COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN
    AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE HDL Code OR THE USE OR OTHER DEALINGS IN THE HDL Code.

    Description: -This component converts a HSV Stream to RGB.
                  R => Hue (0 red => 30 yellow => 60 green => 90 turquoise => 120 Blue => 150 pink => 180 red)
                  G => Saturation (0-255)
                  B => Brightness (0-255)
    
    Insertion:   -Right click on the file -> Add to active project
                 -Add NewComponent of this component into your code
                 -Set the parameters according to your application
                 -Connect the camera stream
                 
    Example:     Camera_CSI_Example
 */

Component HSV2RGB_Filter
(
    Include
    (
        Image_Data_Package.all,
    );
    Generic
    (
        CLK_Edge    : BOOLEAN := true; --true = rising edge
    );
    iStream    : in   rgb_stream;
    oStream    : out  rgb_stream;
)
{
    SIGNAL oStream_buf : rgb_stream;
    SIGNAL iStream_buf : rgb_stream;
    
    oStream.New_Pixel <= iStream.New_Pixel;
    
    SIGNAL New_Pixel_Buf : STD_LOGIC;
    New_Pixel_Buf <= iStream.New_Pixel when CLK_Edge else NOT iStream.New_Pixel;
    
    Process ()
    {
        If(rising_edge(New_Pixel_Buf))
        {
            iStream_buf <= iStream;
            
            oStream.R <= oStream_buf.R;
            oStream.G <= oStream_buf.G;
            oStream.B <= oStream_buf.B;
            oStream.Column <= oStream_buf.Column;
            oStream.Row <= oStream_buf.Row;
            
            oStream_buf.Column <= iStream_buf.Column;
            oStream_buf.Row    <= iStream_buf.Row;
            
            variable h_int, s_int, v_int : integer range 0 to 255;
            variable r_value : integer range 0 to 255;
            variable g_value : integer range 0 to 255;
            variable b_value : integer range 0 to 255;
            
            -- rgb conversion from ycc
            h_int := to_integer(unsigned(iStream_buf.R));
            s_int := to_integer(unsigned(iStream_buf.G));
            v_int := to_integer(unsigned(iStream_buf.B));
            
            If(s_int = 0)
            {
                r_value := v_int;
                g_value := v_int;
                b_value := v_int;
            }
            Else
            {
                VARIABLE i : INTEGER range 0 to 6 := 0;
                VARIABLE f : INTEGER range 0 to 30 := 0;
                VARIABLE p : INTEGER range 0 to 255 := 0;
                VARIABLE q : INTEGER range 0 to 255 := 0;
                VARIABLE t : INTEGER range 0 to 255 := 0;
                i := h_int/30;
                f := h_int mod 30;
                p := (v_int * (255-s_int))/255;
                q := (v_int * (255-((s_int*f)/29)))/255;
                t := (v_int * (255-((s_int*(29-f))/29)))/255;
                Case(i)
                {
                    When(0)
                    {
                        r_value := v_int;
                        g_value := t;
                        b_value := p;
                    }
                    When(1)
                    {
                        r_value := q;
                        g_value := v_int;
                        b_value := p;
                    }
                    When(2)
                    {
                        r_value := p;
                        g_value := v_int;
                        b_value := t;
                    }
                    When(3)
                    {
                        r_value := p;
                        g_value := q;
                        b_value := v_int;
                    }
                    When(4)
                    {
                        r_value := t;
                        g_value := p;
                        b_value := v_int;
                    }
                    When(others)
                    {
                        r_value := v_int;
                        g_value := p;
                        b_value := q;
                    }
                }
            }
            
            -- perform limiting
            
            If(r_value <= 255)
            {
                oStream_buf.R <= std_logic_vector(TO_UNSIGNED(r_value,8));
            }
            Else
            {
                oStream_buf.R <= std_logic_vector(to_unsigned(255, 8));
            }

            If(g_value <= 255)
            {
                oStream_buf.G <= std_logic_vector(TO_UNSIGNED(g_value,8));
            }
            Else
            {
                oStream_buf.G <= std_logic_vector(to_unsigned(255, 8));
            }
            
            If(b_value <= 255)
            {
                oStream_buf.B <= std_logic_vector(TO_UNSIGNED(b_value,8));
            }
            Else
            {
                oStream_buf.B <= std_logic_vector(to_unsigned(255, 8));
            }
        }
    }
}
