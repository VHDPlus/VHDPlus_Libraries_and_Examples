/*
    Copyright (c) 2021 Protop Solutions UG. All right reserved.
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of
    this hdl code and associated documentation files (the "HDL Code"), to deal in the
    HDL Code without restriction, including without limitation the rights to use,
    copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
    HDL Code, and to permit persons to whom the HDL Code is furnished to do so,
    subject to the following conditions:

    The above copyright notice and this permission notice shall be included in all
    copies or substantial portions of the HDL Code.

    THE HDL Code IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
    FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
    COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN
    AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE HDL Code OR THE USE OR OTHER DEALINGS IN THE HDL Code.

    Insertion: -Right click on Logic_Analyzer_SDRAM.vhdp -> Add to project
               -Add NewComponent of Logic_Analyzer_SDRAM in your code
               -Right click the name Logic_Analyzer_SDRAM next to NewComponent and create the needed signal
    
    Function:  -Saves "Inputs" with the rate of 150MHz/Rate_Div while Run is '1'
               -Is limited because of the SDRAM speed, but can save a lot of samples
               -Values can be read with Address and Outputs afterwards
 */

Component Logic_Analyzer_SDRAM
(
    Generic
    (
        Max_Samples    : NATURAL := 3000000; --3Mio samples
        Channels       : NATURAL := 16;
    );
    
    Rate_Div    : IN  NATURAL range 8 to 150000000 := 150; --150MHz clock/150 = 1Mio samples per second
    
    Run         : IN  STD_LOGIC := '0'; --Saves data while Run is '1'
    Full        : OUT STD_LOGIC := '0'; --changes from '0' to '1' when ram is full -> Set Run to '0'
    
    Inputs      : IN  STD_LOGIC_VECTOR(Channels-1 downto 0) := (others => '0');
    
    Address     : IN  NATURAL range 0 to Max_Samples := 0;   --Read the values
    Outputs     : OUT STD_LOGIC_VECTOR(Channels-1 downto 0); --Takes some cycles at 150MHz to read (No problem with the 12MHz base clock)
    
    sdram_addr  : OUT std_logic_vector (11 downto 0);
    sdram_ba    : OUT std_logic_vector (1 downto 0);
    sdram_cas_n : OUT std_logic;
    sdram_dq    : INOUT std_logic_vector (15 downto 0) := (others => '0');
    sdram_dqm   : OUT std_logic_vector (1 downto 0);
    sdram_ras_n : OUT std_logic;
    sdram_we_n  : OUT std_logic;
    sdram_clk   : OUT std_logic;
)
{
    SIGNAL sample_clk_en : STD_LOGIC := '0';

    Process ()
    {
        If(rising_edge(SDRAM_Interface_CLK_150_Out))
        {
            VARIABLE div_cntr : NATURAL range 0 to 150000000 := 0;
            If(div_cntr = Rate_Div-1)
            {
                div_cntr := 0;
                sample_clk_en <= '1';
            }
            Else
            {
                sample_clk_en <= '0';
                div_cntr := div_cntr + 1;
            }
        }
    }

    Generate (if Channels < 17)
    {
        sdram_data_i(Channels-1 downto 0) <= Inputs;
        Outputs <= sdram_data_o(Channels-1 downto 0);
        
        Process ()
        {
            If(rising_edge(SDRAM_Interface_CLK_150_Out))
            {
                Thread
                {
                    VARIABLE in_addr : NATURAL range 0 to Max_Samples := 0;
                    If(Run = '0')
                    {
                        in_addr := 0;
                        Full <= '0';
                        SIGNAL sdram_data_o : STD_LOGIC_VECTOR(15 downto 0);
                        NewFunction Read_SDRAM (sdram_data_o, Address, SDRAM_Interface_Address, SDRAM_Interface_Read_Enable, SDRAM_Interface_Read_Data, SDRAM_Interface_Busy);
                    }
                    Else
                    {
                        For(in_addr := 0; in_addr < Max_Samples AND Run = '1'; in_addr := in_addr + 1)
                        {
                            SIGNAL sdram_data_i : STD_LOGIC_VECTOR(15 downto 0);
                            NewFunction Write_SDRAM (sdram_data_i, in_addr, SDRAM_Interface_Address, SDRAM_Interface_Write_Enable, SDRAM_Interface_Write_Data, SDRAM_Interface_Busy);
                            While(sample_clk_en = '0'){}
                        }
                        Full <= '1';
                    }
                }
            }
        }
    }
    
    Generate (if Channels > 16)
    {
        Generate (for i in 0 to Channels/16)
        {
            Generate (if i = Channels/16)
            {
                Outputs(Channels-1 downto 16*i) <= sdram_data_o_a(16*i+15 downto 16*i);
                sdram_data_i_a(16*i+15 downto 16*i) <= Inputs(Channels-1 downto 16*i);
            }
            Generate (if i < Channels/16)
            {
                Outputs(16*i+15 downto 16*i) <= sdram_data_o_a(16*i+15 downto 16*i);
                sdram_data_i_a(16*i+15 downto 16*i) <= Inputs(16*i+15 downto 16*i);
            }
        }
        
        Process ()
        {
            If(rising_edge(SDRAM_Interface_CLK_150_Out))
            {
                Thread
                {
                    VARIABLE in_addr : NATURAL range 0 to Max_Samples := 0;
                    If(Run = '0')
                    {
                        in_addr := 0;
                        Full <= '0';
                        SIGNAL sdram_data_o_a : STD_LOGIC_VECTOR((Channels/16 + 1)*16-1 downto 0);
                        For(VARIABLE word : NATURAL range 0 to Channels/16+1 := 0; word < Channels/16+1; word := word + 1)
                        {
                            NewFunction Read_SDRAM (sdram_data_o(16*word+15 downto 16*word), Address, SDRAM_Interface_Address, SDRAM_Interface_Read_Enable, SDRAM_Interface_Read_Data, SDRAM_Interface_Busy);
                        }
                    }
                    Else
                    {
                        For(in_addr := 0; in_addr < Max_Samples AND Run = '1'; in_addr := in_addr + 1)
                        {
                            SIGNAL sdram_data_i_a : STD_LOGIC_VECTOR((Channels/16 + 1)*16-1 downto 0);
                            For(word  := 0; word < Channels/16+1; word := word + 1)
                            {
                                NewFunction Write_SDRAM (sdram_data_i(16*word+15 downto 16*word), in_addr, SDRAM_Interface_Address, SDRAM_Interface_Write_Enable, SDRAM_Interface_Write_Data, SDRAM_Interface_Busy);
                            }
                            While(sample_clk_en = '0'){}
                        }
                        Full <= '1';
                    }
                }
            }
        }
    }
    
    Connections
    {
        sdram_addr,
        sdram_ba,
        sdram_cas_n,
        sdram_clk,
        sdram_ras_n,
        sdram_we_n,
        sdram_dqm,
        sdram_dq,
    }
    
    SIGNAL SDRAM_Interface_CLK_150_Out  : std_logic;
    SIGNAL SDRAM_Interface_Address      : std_logic_vector (21 downto 0) := (others => '0');
    SIGNAL SDRAM_Interface_Write_Enable : std_logic := '0';
    SIGNAL SDRAM_Interface_Write_Data   : std_logic_vector (15 downto 0) := (others => '0');
    SIGNAL SDRAM_Interface_Read_Enable  : std_logic := '0';
    SIGNAL SDRAM_Interface_Read_Data    : std_logic_vector (15 downto 0) := (others => '0');
    SIGNAL SDRAM_Interface_Read_Valid   : std_logic := '0';
    SIGNAL SDRAM_Interface_Busy         : std_logic := '0';
    SIGNAL SDRAM_Interface_sdram_cke    : std_logic := '1';
    SIGNAL SDRAM_Interface_sdram_cs_n   : std_logic := '0';
    NewComponent SDRAM_Interface
    (
        Reset        => '0',
        CLK_150_Out  =>SDRAM_Interface_CLK_150_Out,
        Address      =>SDRAM_Interface_Address,
        Write_Enable =>SDRAM_Interface_Write_Enable,
        Write_Data   =>SDRAM_Interface_Write_Data,
        Read_Enable  =>SDRAM_Interface_Read_Enable,
        Read_Data    =>SDRAM_Interface_Read_Data,
        Read_Valid   =>SDRAM_Interface_Read_Valid,
        Busy         =>SDRAM_Interface_Busy,
        sdram_addr   =>sdram_addr,
        sdram_ba     =>sdram_ba,
        sdram_cas_n  =>sdram_cas_n,
        sdram_cke    =>SDRAM_Interface_sdram_cke,
        sdram_cs_n   =>SDRAM_Interface_sdram_cs_n,
        sdram_dq     =>sdram_dq,
        sdram_dqm    =>sdram_dqm,
        sdram_ras_n  =>sdram_ras_n,
        sdram_we_n   =>sdram_we_n,
        sdram_clk    =>sdram_clk,
    );
    
}