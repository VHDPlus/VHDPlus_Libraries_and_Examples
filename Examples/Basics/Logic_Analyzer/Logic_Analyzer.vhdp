--Logic Analyzer with max. 200MHz
--Tested with Core MAX10 and Serial Plotter of the VHDPlus IDE

--Press the button to start
--Set the trigger channel and the trigger edge

--Settings: (Send this strings with the Serial Monitor)
--r25000000   --Sets sample rate to 25 Mega Samples (r+sample rate)
--s10000      --Sets the number of samples to 10k
--c3          --Sets trigger channel to 3 (starts recording on rising or falling edge of this channel)
--f1          --Sets trigger to falling (f1 = falling, f0 = rising)
--e2          --Enables channel 2 (This channel will be shown on the plotter)
--d4          --Disables channel 4 (This channel will not be shown on the plotter)

Main
(
    Button      : IN STD_LOGIC;
    
    Inputs      : IN STD_LOGIC_VECTOR (8-1 downto 0) := (others => '0');
    
    Test_Signal : OUT STD_LOGIC := '0';  --50khz signal
    Test_TX     : OUT STD_LOGIC;         --tx signal can be used as to test logic analyzer
    
    TX          : OUT STD_LOGIC;
    RX          : IN  STD_LOGIC;
)
{
    CONSTANT GraphName : STD_LOGIC_VECTOR := s"Ch ";
    CONSTANT TimeUnit  : STD_LOGIC_VECTOR := s"ns";
    CONSTANT NameChars : NATURAL := 5;  --"Ch " + 2 digits
    
    CONSTANT Max_Samples   : NATURAL := 20000;
    CONSTANT Start_Offset  : NATURAL := 50;
    CONSTANT Channels      : NATURAL := 8;          --Only shows correct name from 0-19
    CONSTANT CLK_Frequency : NATURAL := 12000000;   --12MHz base clock
    CONSTANT HS_CLK_Freq   : NATURAL := 200000000;  --200MHz high speed clock
    CONSTANT Baud_Rate     : NATURAL := 1000000;    --1 mega baud
    CONSTANT Trig_Timeout  : NATURAL := HS_CLK_Freq; --Show output after 1s if no trigger point
    
    SIGNAL Sample_Rate    : NATURAL range 1 to HS_CLK_Freq   := 25000000;  --25MHz default
    SIGNAL Trig_Falling   : NATURAL range 0 to 1             := 1;         --falling edge
    SIGNAL Trig_Channel   : NATURAL range 0 to Channels      := 1;         --Trigger on channel 1
    SIGNAL Enable_Channel : STD_LOGIC_VECTOR (Channels-1 downto 0) := (others => '1'); --Enable all channels
    SIGNAL Samples        : NATURAL range 1 to Max_Samples   := 5000;      --5000 samples
    
    SIGNAL CLK_200 : STD_LOGIC;
    NewComponent PLL
    (
        inclk0 => CLK,
        c0     => CLK_200,
    );
    
    Process Capture ()
    {
        If(rising_edge(CLK_200))    --High speed part -> find trigger edge
        {
            Thread
            {
                --Wait until button is pressed
                While(Button = '0')
                {
                    Wait(10ms);
                }
                While(Button = '1')
                {
                    Wait(10ms);
                }
                
                --Find trigger edge
                If(Trig_Channel < Channels)
                {
                    VARIABLE timeout_cntr : NATURAL range 0 to Trig_Timeout := 0;
                    timeout_cntr := 0;
                    --For rising edge wait for '0'
                    While((Inputs(Trig_Channel) = '0' XOR Trig_Falling = 0) AND timeout_cntr < Trig_Timeout){ timeout_cntr := timeout_cntr + 1; }
                    --For rising edge wait for '1'
                    While((Inputs(Trig_Channel) = '0' XOR Trig_Falling = 1) AND timeout_cntr < Trig_Timeout){ timeout_cntr := timeout_cntr + 1; }
                }
                
                --Fill logic analyzer with ADC data
                Logic_Analyzer_Run <= '1';
                While(Logic_Analyzer_Full = '0'){}
                Logic_Analyzer_Run <= '0';
                
                --Start transmission to plotter
                SIGNAL Send_Data         : STD_LOGIC := '0';
                SIGNAL Finished_Sending  : STD_LOGIC := '1';
                Send_Data <= '1';
                While(Finished_Sending = '1'){}
                Send_Data <= '0';
                While(Finished_Sending = '0'){}
            }
        }
    }
    
    Process Send ()
    {
        Thread
        {
            While(Send_Data = '0'){}
            Finished_Sending <= '0';
            
            --Send saved data
            For(Logic_Analyzer_Address <= 0; Logic_Analyzer_Address < Samples; Logic_Analyzer_Address <= Logic_Analyzer_Address + 1)
            {
                For(VARIABLE cur_channel : NATURAL := 0; cur_channel < Channels; cur_channel := cur_channel + 1)
                {
                    If(Enable_Channel(cur_channel) = '1')
                    {
                        If(cur_channel > 9) { Plotter_Graph_Name <= GraphName & s"1" & STD_LOGIC_VECTOR(TO_UNSIGNED(cur_channel-10+48, 8)); }
                        Else { Plotter_Graph_Name <= GraphName & STD_LOGIC_VECTOR(TO_UNSIGNED(cur_channel+48, 8)) & x"00"; }
                        If(Logic_Analyzer_Outputs(cur_channel) = '1') { Plotter_Value <= 1; }
                        Else { Plotter_Value <= 0; }
                        Plotter_Time       <= (Logic_Analyzer_Address-Start_Offset) * (1000000000/Sample_Rate);      --Sample rate to nano seconds
                        --Send to plotter
                        Plotter_Send <= '1';
                        While(Plotter_Busy = '0'){}
                        Plotter_Send <= '0';
                        While(Plotter_Busy = '1'){}
                    }
                }
            }
            
            Finished_Sending <= '1';
        }
    }
    
    Process Settings ()
    {
        Thread
        {
            --Read string from console in "readStr"
            NewFunction newString (readStr);                       --Create string
            NewFunction readString (readStr, UART_Interface_RX_Data, UART_Interface_RX_Busy);  --Read uart message in string
            SIGNAL channel_i : INTEGER;
            SIGNAL rate_i    : INTEGER;
            SIGNAL fall_i    : INTEGER;
            SIGNAL ena_i     : INTEGER;
            SIGNAL disa_i    : INTEGER;
            SIGNAL sam_i    : INTEGER;
            --Check if received string is channel or sample rate setting
            rate_i    <= -1;                                       --Set sample rate index to -1 if indexof is skipped
            fall_i    <= -1;
            ena_i     <= -1;
            disa_i    <= -1;
            sam_i     <= -1;
            NewFunction indexOf (readStr, s"c", 1, 0, channel_i);  --Find index of c: "c3" <- 0  "r10000" <- -1
            If(channel_i /= 0)                                     --check if message was channel setting
            {
                NewFunction indexOf (readStr, s"r", 1, 0, rate_i); --Find index of r: "r10000" <- 0
            }
            If(channel_i /= 0 AND rate_i /= 0)
            {
                NewFunction indexOf (readStr, s"f", 1, 0, fall_i); --Find index of t: "f1" <- 0
            }
            If(channel_i /= 0 AND rate_i /= 0 AND fall_i /= 0)
            {
                NewFunction indexOf (readStr, s"e", 1, 0, ena_i); --Find index of e: "e1" <- 0
            }
            If(channel_i /= 0 AND rate_i /= 0 AND fall_i /= 0 AND ena_i /= 0)
            {
                NewFunction indexOf (readStr, s"d", 1, 0, disa_i); --Find index of d: "d1" <- 0
            }
            If(channel_i /= 0 AND rate_i /= 0 AND fall_i /= 0 AND ena_i /= 0 AND disa_i /= 0)
            {
                NewFunction indexOf (readStr, s"s", 1, 0, sam_i); --Find index of s: "s100" <- 0
            }
            If(rate_i = 0 OR channel_i = 0 OR fall_i = 0 OR ena_i = 0 OR disa_i = 0 OR sam_i = 0) --check if received string is channel or sample rate setting
            {
                --Convert received string to value of new sample rate or adc channel
                NewFunction newString (convertStr);                --create string for the number in the message
                NewFunction substring (readStr, 1, readStr.Length-1, convertStr);  --remove first char in message: "c3" -> "3", "r10000" -> "10000"
                SIGNAL receivedValue : NATURAL;
                NewFunction stringToNatural (convertStr, receivedValue); --Convert string to number
                if(rate_i = 0 AND receivedValue > 0 AND receivedValue <= HS_CLK_Freq) --check if sample rate is from 1 - 100,000,000
                {
                    Sample_Rate <= receivedValue;                  --set new sample rate
                }
                Elsif(channel_i = 0 AND receivedValue < Channels)  --check if channel is from 0 - Channel-1
                {
                    Trig_Channel <= receivedValue;                 --set new trigger channel
                }
                Elsif(fall_i = 0 AND receivedValue < 2)            --check if falling/rising edge setting is 0 or 1
                {
                    Trig_Falling <= receivedValue;                 --set new trigger falling/rising edge setting
                }
                Elsif(ena_i = 0 AND receivedValue < Channels)      --check if channel to enable is from 0 - Channels-1
                {
                    Enable_Channel(receivedValue) <= '1';          --set new channel to enable
                }
                Elsif(disa_i = 0 AND receivedValue < Channels)     --check if channel to disable is from 0 - Channels-1
                {
                    Enable_Channel(receivedValue) <= '0';          --set new channel to disable
                }
                Elsif(sam_i = 0 AND receivedValue < Max_Samples)   --check if samples are from < max_samples
                {
                    Samples <= receivedValue;                      --set new samples
                }
            }
        }
    }
    
    --Create 50 kHz signal to test input
    Process Test ()
    {
        Thread
        {
            --A bit lower frequency because Wait and setting the value also take 1 clock cycle
            Test_Signal <= '0';
            Wait(10us);
            Test_Signal <= '1';
            Wait(10us);
        }
    }
    
    --Create a uart test signal (sends "Test\n" on repeat)
    Process ()
    {
        Thread
        {
            NewFunction newConstString (s"Test\n", testStr);
            NewFunction printString (testStr, UART_Interface_TX_Data, UART_Interface_TX_Busy, UART_Interface_TX_Enable);
            Wait(50us);
        }
    }
    
    SIGNAL UART_Interface_TX_Enable     : STD_LOGIC := '0';
    SIGNAL UART_Interface_TX_Busy       : STD_LOGIC := '0';
    SIGNAL UART_Interface_TX_Data       : STD_LOGIC_VECTOR (8-1 DOWNTO 0) := (others => '0');
    SIGNAL UART_Interface_RX_Busy       : STD_LOGIC := '0';
    SIGNAL UART_Interface_RX_Data       : STD_LOGIC_VECTOR (8-1 DOWNTO 0) := (others => '0');
    NewComponent UART_Interface
    (
        CLK_Frequency => CLK_Frequency,
        Baud_Rate     => Baud_Rate,
        OS_Rate       => 12,

        TX            => Test_TX,
        RX            => RX,
        TX_Enable     => UART_Interface_TX_Enable,
        TX_Busy       => UART_Interface_TX_Busy,
        TX_Data       => UART_Interface_TX_Data,
        RX_Data       => UART_Interface_RX_Data,
        RX_Busy       => UART_Interface_RX_Busy,
    );
    
    Logic_Analyzer_Rate_Div <= HS_CLK_Freq/Sample_Rate;
    
    SIGNAL Logic_Analyzer_Rate_Div      : NATURAL          range 1 to HS_CLK_Freq;
    SIGNAL Logic_Analyzer_Run           : STD_LOGIC := '0';
    SIGNAL Logic_Analyzer_Full          : STD_LOGIC := '0';
    SIGNAL Logic_Analyzer_Address       : NATURAL          range 0 to Max_Samples := 0;
    SIGNAL Logic_Analyzer_Outputs       : STD_LOGIC_VECTOR (Channels-1 downto 0);
    NewComponent Logic_Analyzer
    (
        Max_Samples   => Max_Samples,
        CLK_Frequency => HS_CLK_Freq,
        Channels      => Channels,
        Start_Offset  => Start_Offset,

        CLK           => CLK_200,
        Rate_Div      => Logic_Analyzer_Rate_Div,
        Samples       => Samples,
        Start         => Logic_Analyzer_Run,
        Full          => Logic_Analyzer_Full,
        Inputs        => Inputs,
        Address       => Logic_Analyzer_Address,
        Outputs       => Logic_Analyzer_Outputs,
    );

    SIGNAL Plotter_Value         : INTEGER := 0;
    SIGNAL Plotter_Time          : INTEGER := 0;
    SIGNAL Plotter_Graph_Name    : STD_LOGIC_VECTOR (NameChars*8-1 downto 0) := (others => '0');
    SIGNAL Plotter_Send          : STD_LOGIC := '0';
    SIGNAL Plotter_Busy          : STD_LOGIC := '0';
    NewComponent Serial_Plotter_Interface
    (
        UseTime       => true,
        TimeUnit      => TimeUnit,
        NameChars     => NameChars,
        CLK_Frequency => CLK_Frequency,
        Baud_Rate     => Baud_Rate,

        Value         => Plotter_Value,
        Time          => Plotter_Time,
        Graph_Name    => Plotter_Graph_Name,
        Send          => Plotter_Send,
        Busy          => Plotter_Busy,
        TX            => TX,
    );
}