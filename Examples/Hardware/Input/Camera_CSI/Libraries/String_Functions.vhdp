Class
{
    --Creates the RAM instance for the string
    StepFunction newString
    (
        oString : String_Type;
    )
    {
        NewComponent Single_Port_RAM
        (
            Width    => 256,
            
            Write    => oString.Write,
            Address  => oString.Address,
            Data_IN  => oString.Data_IN,
            Data_OUT => oString.Data_OUT,
        );
        
        SIGNAL oString : String_Type;
        
        FunctionContent
        {
            
        }
    }
    
    --Creates a ROM instance with the string value (e.g. "example")
    StepFunction assignString
    (
        strValue : STD_LOGIC_VECTOR;
        oString  : String_Type;
    )
    {
        NewComponent Single_Port_ROM
        (
            Data     => strValue,
            
            Length   => oString.Length,
            Address  => oString.Address,
            Data_OUT => oString.Data_OUT,
        );
        
        SIGNAL oString : String_Type;
        
        FunctionContent
        {
            
        }
    }
    
    --iString1 + iString2 = oString
    --String = RAM interface + string length
    StepFunction stringConcat
    (
        iString1 : String_Type;
        
        iString2 : String_Type;
        
        oString  : String_Type;
    )
    {
        oString.Write <= '1';
        StepFor(oString.Length <= 0; oString.Length < iString1.Length; oString.Length <= oString.Length + 1)
        {
            iString1.Address <= oString.Length;
            Step
            {
                oString.Address <= oString.Length;
                oString.Data_IN <= iString1.Data_OUT;
            }
        }
        StepFor(; oString.Length < iString1.Length + iString2.Length; oString.Length <= oString.Length + 1)
        {
            iString2.Address <= oString.Length - iString1.Length;
            Step
            {
                oString.Address <= oString.Length;
                oString.Data_IN <= iString2.Data_OUT;
            }
        }
        oString.Write <= '0';
    }
    
    --oString + iString = oString
    StepFunction stringAppend
    (
        iString  : String_Type;
        
        oString  : String_Type;
    )
    {
        oString.Write <= '1';
        StepFor(VARIABLE appendCount : NATURAL range 0 to 255 := 0; appendCount < iString.Length; appendCount := appendCount + 1)
        {
            iString.Address <= appendCount;
            Step
            {
                oString.Address <= oString.Length;
                oString.Data_IN <= iString.Data_OUT;
                oString.Length  <= oString.Length + 1;
            }
        }
        oString.Write <= '0';
    }
    
    --oString + iChar = oString (e.g. iChar = s"\n")
    StepFunction charAppend
    (
        iChar   : STD_LOGIC_VECTOR(7 downto 0);
        
        oString : String_Type;
    )
    {
        oString.Write   <= '1';
        oString.Address <= oString.Length;
        oString.Data_IN <= iChar;
        oString.Length  <= oString.Length + 1;
        Step
        {
            oString.Write <= '0';
        }
    }
    
    --Converts natural to string
    --Important: Needs BinaryToBcd and BinaryToBsdDigit
    StepFunction naturalToString
    (
        iNatural : NATURAL;
        oString  : String_Type;
        
        bcdEna   : STD_LOGIC;
        bcdBus   : STD_LOGIC;
        bcdBin   : STD_LOGIC_VECTOR;
        bcdDec   : STD_LOGIC_VECTOR;
    )
    {
        bcdBin <= STD_LOGIC_VECTOR(TO_UNSIGNED(iNatural, bcdBin'LENGTH));
        bcdEna <= '1';
        While(bcdBus = '0'){}
        bcdEna <= '0';
        While(bcdBus = '1'){}
        oString.Length <= 0;
        StepFor(VARIABLE charConvCounter : NATURAL range 0 to bcdDec'LENGTH/4 := bcdDec'LENGTH/4-1; charConvCounter > 0; charConvCounter := charConvCounter - 1)
        {
            If(oString.Length > 0 OR charConvCounter = 1 OR bcdDec((charConvCounter*4 - 1) downto (charConvCounter*4 - 4)) /= "0000")
            {
                oString.Write   <= '1';
                oString.Address <= oString.Length;
                oString.Data_IN <= "0011" & bcdDec((charConvCounter*4 - 1) downto (charConvCounter*4 - 4));
                oString.Length  <= oString.Length + 1;
            }
        }
        oString.Write  <= '0';
    }
    
    --Converts natural to string
    --Important: Creates BinaryToBcd with Signals -> bcdEna, ... are only for naming the signals
    StepFunction naturalToStringInst
    (
        iNatural : NATURAL;
        oString  : String_Type;
        
        bcdEna   : STD_LOGIC;
        bcdBus   : STD_LOGIC;
        bcdBin   : STD_LOGIC_VECTOR;
        bcdDec   : STD_LOGIC_VECTOR;
    )
    {
        SIGNAL bcdEna : STD_LOGIC;
        SIGNAL bcdBin : STD_LOGIC_VECTOR (32-1 DOWNTO 0);
        SIGNAL bcdBus : STD_LOGIC;
        SIGNAL bcdDec : STD_LOGIC_VECTOR (10*4-1 DOWNTO 0);
        
        NewComponent BinaryToBcd
        (
            Bits   => 32,
            Digits => 10,
            
            Enable => bcdEna,
            Binary => bcdBin,
            Busy   => bcdBus,
            BCD    => bcdDec,
        );
        
        FunctionContent
        {
            bcdBin <= STD_LOGIC_VECTOR(TO_UNSIGNED(iNatural, bcdBin'LENGTH));
            bcdEna <= '1';
            While(bcdBus = '0'){}
            bcdEna <= '0';
            While(bcdBus = '1'){}
            oString.Length <= 0;
            StepFor(VARIABLE charConvCounter : NATURAL range 0 to bcdDec'LENGTH/4 := bcdDec'LENGTH/4-1; charConvCounter > 0; charConvCounter := charConvCounter - 1)
            {
                If(oString.Length > 0 OR charConvCounter = 1 OR bcdDec((charConvCounter*4 - 1) downto (charConvCounter*4 - 4)) /= "0000")
                {
                    oString.Write   <= '1';
                    oString.Address <= oString.Length;
                    oString.Data_IN <= "0011" & bcdDec((charConvCounter*4 - 1) downto (charConvCounter*4 - 4));
                    oString.Length  <= oString.Length + 1;
                }
            }
            oString.Write  <= '0';
        }
    }
    
    --Converts string to natural ("12345\r\n" is converted to 12345 but "\n123" to 0)
    StepFunction stringToNatural
    (
        iString  : String_Type;
        oNatural : NATURAL;
    )
    {
        oNatural <= 0;
        StepFor(VARIABLE convertCharCount : NATURAL range 0 to 255 := 0; convertCharCount < iString.Length; convertCharCount := convertCharCount + 1)
        {
            iString.Address <= convertCharCount;
            Step
            {
                If(iString.Data_OUT > x"2F" AND iString.Data_OUT < x"3A")
                {
                    oNatural <= oNatural*10 + TO_INTEGER(UNSIGNED(iString.Data_OUT)) - 48;
                }
                else
                {
                    convertCharCount := iString.Length;
                }
            }
        }
    }
    
    --Writes a part of a string in a different string
    --"123456789" startindex = 3 length = 3 => "456"
    StepFunction substring
    (
        iString    : String_Type;
        startindex : NATURAL;
        length     : NATURAL;
        oString    : String_Type;
    )
    {
        oString.Length <= 0;
        StepFor(VARIABLE substringCount : NATURAL range 0 to 255 := startindex; substringCount < length+startindex; substringCount := substringCount + 1)
        {
            iString.Address <= substringCount;
            Step
            {
                oString.Write   <= '1';
                oString.Address <= oString.Length;
                oString.Data_IN <= iString.Data_OUT;
                oString.Length  <= oString.Length + 1;
            }
        }
        oString.Write  <= '0';
    }
    
    --Finds the first appearance of the character/string after the startindex
    --"123456789" value = s"23" valueLen = 2 startIndex = 0 -> index = 1
    StepFunction indexOf
    (
        iString    : String_Type;
        value      : STD_LOGIC_VECTOR;
        valueLen   : NATURAL;
        startIndex : NATURAL;
        index      : INTEGER;
    )
    {
        CONSTANT valueVar : STD_LOGIC_VECTOR(valueLen*8-1 downto 0) := value;
        index <= -1;
        StepFor(VARIABLE indexCount : NATURAL range 0 to 255 := startIndex; indexCount < iString.Length; indexCount := indexCount + 1)
        {
            iString.Address <= indexCount;
            Step
            {
                StepFor(VARIABLE stringCheck : NATURAL range 1 to valueLen := 1; stringCheck < valueLen AND iString.Data_OUT = valueVar((valueLen-stringCheck)*8+7 downto (valueLen-stringCheck)*8); stringCheck := stringCheck + 1)
                {
                    iString.Address <= indexCount+stringCheck;
                }

                If(iString.Data_OUT = valueVar((valueLen-stringCheck)*8+7 downto (valueLen-stringCheck)*8))
                {
                    index      <= indexCount;
                    indexCount := iString.Length;
                }
            }
        }
    }
    
    --Converts string to one STD_LOGIC_VECTOR (e.g. for led matrix or LCD display)
    StepFunction stringToVector
    (
        iString       : String_Type;
        oVector       : STD_LOGIC_VECTOR;
    )
    {
        iString.Write <= '0';
        StepFor(VARIABLE StV_i : NATURAL range 0 to 255 := 0; StV_i < iString.Length; StV_i := StV_i + 1)
        {
            iString.Address <= StV_i;
            Step
            {
                oVector(((oVector'LENGTH/8)-StV_i)*8-1 downto ((oVector'LENGTH/8)-StV_i-1)*8) <= iString.Data_OUT;
            }
        }
    }
}